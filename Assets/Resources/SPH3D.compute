#include "utils/structures.cginc"
#include "utils/buffers.cginc"
#include "utils/constants.cginc"

#include "collison_detection/wallBoundray.cginc"

#pragma kernel DensityCS
#pragma kernel ForceCS
#pragma kernel IntegrateCS

// Keep the number of threads one-dimensional for simplicity
#define THREAD_SIZE_X 1024

// ▼ ★ Main body from here --------------------------------------

#include "kernels/Densitykernel.cginc"
#include "kernels/pressurekernel.cginc"
#include "kernels/gradPressurekernel.cginc"
#include "kernels/viscositykernel.cginc"
#include "kernels/surfaceTensionkernel.cginc"


// ▼ Kernel function

//--------------------------------------------------------------------------------------
// Density andd pressure calculation
//--------------------------------------------------------------------------------------

void Pressure(uint P_ID) {

	float  P_density = _ParticlesDensityBuffer[P_ID].density;	// Density calculated earlier
	float  P_pressure = CalculatePressure(P_density);	// Pressure calculation

	// Update pressure buffer
	_ParticlesPressureBuffer[P_ID].pressure = P_pressure;
}
[numthreads(THREAD_SIZE_X, 1, 1)]
void DensityCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x;	// Particle ID currently being processed

	float h_sq = _Smoothlen * _Smoothlen;
	float3 P_position = _ParticlesBuffer[P_ID].position;

	// TODO:convert To hash
	// Neighborhood search(O(n^2))
	float density = CalculateDensity(particleRadius);
	for (uint N_ID = 0; N_ID < (uint)_neighbourTracker[DTid.x]; N_ID++) {
  		int neighbourIndex = _neighbourList[DTid.x * maximumParticlesPerCell * 8 + N_ID];
		float3 N_position = _ParticlesBuffer[neighbourIndex].position;
		float3 diff = N_position - P_position;	// Particle distance
		float r_sq = dot(diff, diff);			// Particle distance squared
		density += CalculateDensity(r_sq);	// No need to take the route
	}

	// Update density buffer
	_ParticlesDensityBuffer[P_ID].density = density;
	Pressure(P_ID);
}


//--------------------------------------------------------------------------------------
// Calculation of force (acceleration)
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void ForceCS(uint3 DTid : SV_DispatchThreadID) {
	uint P_ID = DTid.x; // Particle ID currently being processed

	float3 P_position = _ParticlesBuffer[P_ID].position;
	float3 P_velocity = _ParticlesBuffer[P_ID].velocity;
	float  P_density = _ParticlesDensityBuffer[P_ID].density;		// Density calculated earlier
	float  P_pressure = _ParticlesPressureBuffer[P_ID].pressure;	// The pressure calculated earlier

	const float h_sq = _Smoothlen * _Smoothlen;

	float3 press = float3(0, 0, 0);
	float3 visco = float3(0, 0, 0);
	// float3 surfaceTension = float3(0, 0, 0);
	for (uint N_ID = 0; N_ID < (uint)_neighbourTracker[DTid.x]; N_ID++) {
		  int neighbourIndex = _neighbourList[DTid.x * maximumParticlesPerCell * 8 + N_ID];

		float3 N_position = _ParticlesBuffer[neighbourIndex].position;

		float3 diff = N_position - P_position;
		float r_sq = dot(diff, diff);

		float  N_density = _ParticlesDensityBuffer[neighbourIndex].density;
		float  N_pressure = _ParticlesPressureBuffer[neighbourIndex].pressure;
		float3 N_velocity = _ParticlesBuffer[neighbourIndex].velocity;
		float  r = sqrt(r_sq);

		// Pressure term
		press += CalculateGradPressure(r, P_pressure, N_pressure, N_density, diff);

		// sticky item
		visco += CalculateLapVelocity(r, P_velocity, N_velocity, N_density);


	}
	
	
	// Integration
	float3 force = press + _Viscosity * visco ;

	// Acceleration buffer update
	_ParticlesForceBuffer[P_ID].acceleration = force / P_density;
}

//--------------------------------------------------------------------------------------
// Update the position by performing collision detection etc.
//--------------------------------------------------------------------------------------
[numthreads(THREAD_SIZE_X, 1, 1)]
void IntegrateCS(uint3 DTid : SV_DispatchThreadID) {
	const unsigned int P_ID = DTid.x; // Particle ID currently being processed

	// Position and speed before update
	float3 position = _ParticlesBuffer[P_ID].position;
	float3 velocity = _ParticlesBuffer[P_ID].velocity;
	float3 acceleration = _ParticlesForceBuffer[P_ID].acceleration;	// Calculated acceleration

	// if (distance(position, _MousePos.xy) < _MouseRadius && _MouseDown) {
	// 	float3 dir = position - _MousePos.xy;
	// 	float pushBack = _MouseRadius-length(dir);
	// 	acceleration += 100 * pushBack * normalize(dir);
	// }
    

	// Here to write collision detection -----

	// Wall boundary
	acceleration=penaltyWallboundray(acceleration,position);

	// Gravity addition
	//TODO:must _Gravity*_mass
	acceleration += _Gravity;

	// Update the next particle position with the forward Euler method
	velocity += _TimeStep * acceleration;
	position += _TimeStep * velocity;

	// Particle buffer update
	_ParticlesBuffer[P_ID].position = position;
	_ParticlesBuffer[P_ID].velocity = velocity;
}


